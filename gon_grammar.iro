#################################################################
## Iro
################################################################ 
##
## * Press Ctrl + '+'/'-' To Zoom in
## * Press Ctrl + S to save and recalculate... 
## * Documents are saved to web storage.
## * Only one save slot supported.
## * Matches cannot span lines.
## * Unicode chars must be defined in \u0000 to \uffff format.
## * All matches must be contained by a single group ( ... )
## * Look behinds not permitted, (?<= or (?<!
## * Look forwards are permitted (?= or (?!
## * Constants are defined as __my_const = (......)
## * The \= format allows unescaped regular expressions
## * Constants referenced by match \= $${__my_const}
## * Constants can reference other constants
## * You are free to delete all the default scopes.
## * Twitter : ainslec , Web: http://eeyo.io/iro
##
################################################################

name                   = gon
file_extensions []     = gon;

################################################################
## Styles
################################################################
styles [] {

   .array_field : style {
      color     = gold
      textmate_scope        = entity.name.type
   }
   
   .object_field : style {
      color     = yellow
      textmate_scope        = entity.name.type
   }
   
   .regular_field : style {
      color     = green
      textmate_scope        = support.variable
   }

   .bool : style {
      color     = red
      textmate_scope        = constant.language
   }
   
   .number : style {
      color     = cyan
      textmate_scope        = constant.numeric
   }
   
   .comma : style {
      color     = red_2
      textmate_scope        = keyword.operator
   }
   
   .array_brackets : style {
      color     = red_2
      textmate_scope        = keyword.operator
   }
   
   .object_curly_brackets : style {
      color     = red_2
      textmate_scope        = keyword.operator
   }
   
   .text : style {
      color     = brown
      textmate_scope        = string
   }
   
   .comment : style {
      color     = grey
      textmate_scope        = comment
   }
   
   .illegal : style {
      color            = white
      background_color = red
      textmate_scope        = invalid
   }
}

################################################################
## Contexts
################################################################
contexts[] {
 main : context {
 
   : include "comments" ;
   
   : inline_push {
      description = object_field
      regex          \= (((?:"(?:[^"\\]|\\.)*")|(?:[^(\s\,\[\{\}\]#\/$)]+))(?=(\s*)\{))
      styles []       = .object_field;
      : pop {
         description = object_closing_bracket
         regex       \= (\})
         styles []    = .object_curly_brackets;
      }
      : pattern {
         description = object_opening_bracket
         regex       \= (\{)
         styles []    = .object_curly_brackets;
      }
      : include "main" ;
   }
   
   : inline_push {
      description = array_field
      regex          \= (((?:"(?:[^"\\]|\\.)*")|(?:[^(\s\,\[\{\}\]#\/$)]+))(?=(\s*)\[))
      styles []       = .array_field;
      : pop {
         description = array_closing_bracket
         regex       \= (\])
         styles []    = .array_brackets;
      }
      : pattern {
         description = array_opening_bracket
         regex       \= (\[)
         styles []    = .array_brackets;
      }
      : pattern {
         description = comma
         regex       \= (,)
         styles []    = .comma;
      }
      : include "array";
      : include "object";
      : include "comments";
      : include "bool" ;
      : include "number" ;
      : include "string" ;
   }
   
   : inline_push {
      description = regular_field
      regex          \= (((?:"(?:[^"\\]|\\.)*")|(?:[^(\s\,\[\{\}\]#\/$)]+)))
      styles []       = .regular_field;
      : pop {
         regex       \= (?:(true|false)|([-+]?[0-9]*\.?[0-9]+)(?=\s|\,|\}|\]|$)|((?:"(?:[^"\\]|\\.)*")|(?:[^(\s\,\[\{\}\]#\/$)]+)))
         styles []       = .bool, .number, .text;
      }
      : include "comments";
   }
   
   : pattern {
      description = illegal_opening_curly_brackets
      regex          \= (\{)
      styles []       = .illegal;
   }
   
   : pattern {
      description = illegal_closing_curly_brackets
      regex          \= (\})
      styles []       = .illegal;
   }
   
   : pattern {
      description = illegal_opening_brackets
      regex          \= (\[)
      styles []       = .illegal;
   }
   
   : pattern {
      description = illegal_opening_brackets
      regex          \= (\])
      styles []       = .illegal;
   }
 }
 
 comments : context {
 
   : inline_push {
      description = multiline_comment
      regex          \= (/\*)
      styles []       = .comment;
      default_style   = .comment
      : pop {
         regex       \= (\*/(\s|$)*)
         styles []    = .comment;
      }
   }
   
   : pattern {
      description = regular_comment
      regex          \= (//.*)
      styles []       = .comment;
   }
   
   : pattern {
      description = hash_comment
      regex          \= (#.*)
      styles []       = .comment;
   }
 }
 
 object : context {
   : inline_push {
      description = object_opening_bracket
      regex          \= (\{)
      styles []       = .object_curly_brackets;
      : pop {
         description = object_closing_bracket
         regex       \= (\})
         styles []    = .object_curly_brackets;
      }
      : pattern {
         description = illegal_opening_curly_brackets
         regex          \= (\{)
         styles []       = .illegal;
      }
      : include "main" ;
   }
 }
 
 array : context {
   : inline_push {
      description = array_opening_bracket
      regex          \= (\[)
      styles []       = .array_brackets;
      : pop {
         description = array_closing_bracket
         regex       \= (\])
         styles []    = .array_brackets;
      }
      : pattern {
         description = comma
         regex       \= (,)
         styles []    = .comma;
      }
      : pattern {
         description = illegal_opening_brackets
         regex          \= (\[)
         styles []       = .illegal;
      }
      : include "array";
      : include "object";
      : include "comments";
      : include "bool" ;
      : include "number" ;
      : include "string" ;
   }
 }
 
 string : context {
   : pattern {
      description = string
      regex       \= (("(?:[^"\\]|\\.)*")|([^(\s\,\[\{\}\]$)]+))
      styles []   = .text;
   }
 }
 
 number : context {
   : pattern {
      description = number
      regex \= ([-+]?[0-9]*\.?[0-9]+)(?=\s|\,|\}|\]|$)
      styles = .number
   }
 }
 
 bool : context {
   : pattern {
      description = bool
      regex \= (true|false)
      styles = .bool
   }
 }
}